plugins {
    id 'com.android.application'
}

android {
    // Read local.properties
    Properties properties = new Properties()
    InputStream inputStream = project.rootProject.file('local.properties').newDataInputStream() ;
    properties.load( inputStream )

    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "io.agora.chatdemo"
        minSdkVersion 19
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"
        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
        // If you use your own Agora Chat appkey, you should deploy your own App Server.
        // See:
        buildConfigField ("String", "APP_SERVER_URL", "\" http://hk.test.easemob.com/app/chat/user/login\"")

        manifestPlaceholders = [AGORA_CHAT_APPKEY: properties.getProperty("AGORA_CHAT_APPKEY", "******")]
    }

    signingConfigs {
        debug {
            storeFile file(properties.getProperty('KEY_STORE_FILE_PATH', './keystore/sdkdemo.jks'))
            storePassword properties.getProperty('KEY_STORE_PASSWORD', '123456')
            keyAlias = properties.getProperty('KEY_ALIAS', 'easemob')
            keyPassword properties.getProperty('KEY_PASSWORD', '123456')
        }
        release {
            storeFile file(properties.getProperty('KEY_STORE_FILE_PATH', './keystore/sdkdemo.jks'))
            storePassword properties.getProperty('KEY_STORE_PASSWORD', '123456')
            keyAlias = properties.getProperty('KEY_ALIAS', 'easemob')
            keyPassword properties.getProperty('KEY_PASSWORD', '123456')
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }

        debug {
            signingConfig signingConfigs.debug
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildFeatures{
        viewBinding true
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    //ViewModel and LiveData
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    // Google firebase cloud messaging
    // Import the BoM for the Firebase platform
    implementation platform('com.google.firebase:firebase-bom:28.2.1')
    // Declare the dependencies for the Firebase Cloud Messaging and Analytics libraries
    // When using the BoM, you don't specify versions in Firebase library dependencies
    implementation 'com.google.firebase:firebase-messaging'
    implementation 'com.google.firebase:firebase-analytics'
    // room
    implementation "androidx.room:room-runtime:2.2.5"
    annotationProcessor "androidx.room:room-compiler:2.2.5"
    // swipe refresh layout
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    // Glide
    implementation 'com.github.bumptech.glide:glide:4.9.0'
    // fresh and load more
    api 'com.scwang.smartrefresh:SmartRefreshLayout:1.0.5.1'
    // Agora Chat Uikit
    implementation project(path: ':uikit')
}

// ADD THIS AT THE BOTTOM
apply plugin: 'com.google.gms.google-services'